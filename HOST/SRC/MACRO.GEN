; MACRO.GEN

BASE MACRO @Par1
_OFST	defl	@Par1
	ENDM

PDEF MACRO @Par1,@Par2
@Par1	equ	_OFST
_OFST	defl	_OFST + @Par2
	ENDM

MDEF MACRO @Par1,@Par2
_OFST	defl	_OFST - @Par2
@Par1	equ	_OFST
	ENDM

FONTBASE MACRO @Par1
FONTOFST defl @Par1
	ENDM

DEFFONT	MACRO @Par1
	defb FONTOFST
	defb @Par1
FONTOFST defl FONTOFST + @Par1
	ENDM

PWD	MACRO @Par1,@Par2,@Par3,@Par4,@Par5,@Par6
	defb @Par1 + 4
	defb - @Par2
	defb @Par3.shl.1
	defb @Par4 - 32
	defb @Par5 + 128 - 11
	defb - @Par6 + 3
	ENDM

RWy	MACRO @p1,@p2,@p3
 	ld @p2,(iy+@p3)
 	ld @p1,(iy+@p3+1)
 	ENDM

MUL_HLBC MACRO
 	defw 0C3EDh
    ENDM

; ***
;	IN	BC
;	OUT	IX = BC * 8

IX_BCx8 MACRO
	sla	c
	rl b
	sla	c
	rl b
	sla	c
	rl b

	; ix = bc
	; ld ixh,b
	defb 0DDh
	ld h,b

	; ld ixl,c
	defb 0DDh
	ld l,c
    ENDM

; ***
; PF_DIM_X DEPENDENT

COPY_ROW	MACRO
	ldi
	ldi
	ldi
	ldi
	ldi
	ldi
	ldi
	ldi
	ldi
	ldi
	ENDM



OUTI_x_15	MACRO
	outi
	outi
	outi
	outi

	outi
	outi
	outi
	outi

	outi
	outi
	outi
	outi

	outi
	outi
	outi
	ENDM

LDI_x_4	MACRO
	ldi
	ldi
	ldi
	ldi
	ENDM

ALIGN	MACRO	@BASE
s1		defl .NOT.(@BASE - 1)
s2		defl $.AND.s1
s3		defl $ - s2
 if s3
s4		defl @BASE - s3
		defs s4
 else
s4		defl	0
 endc
	ENDM

;	IN	HL = m24 address
;	ACT	set m24 = 0
M24_LD_0       MACRO
	ld (hl),0
	inc hl
	ld (hl),0	
	inc hl
	ld (hl),0	
	ENDM

;	IN	HL -> m24
;		DE = unsigned 16 bit value to add
;	MOD	AF,HL
;	ACT	m24 += DE
M24_ADD_U16    MACRO
	ld a,(hl)
	add a,e
	ld (hl),a
	inc hl
	ld a,(hl)
	adc a,d
	ld (hl),a
	inc hl
	ld a,(hl)
	adc a,0
	ld (hl),a
	ENDM

;	IN	HL = m24 address
;	MOD	HL
;	ACT	m24 = MAX_UINT = 0xffffffh
M24_LD_MAXUINT MACRO
	ld (hl),0ffh
	inc hl
	ld (hl),0ffh
	inc hl
	ld (hl),0ffh
	ENDM

;	IN	HL = m24 address #0
;		DE = m24 address #1
;	MOD	F,BC,DE,HL
;	ACT	copy m24_#0 -> m24_#1
M24_CPY        MACRO
	ldi
	ldi
	ldi	
	ENDM

;	IN	HL -> m24
;	MOD	AF
;	ACT	m24 << 1
;M24_SHF_L    MACRO
;	sla (hl)
;	inc hl
;	rl (hl)
;	inc hl
;	rl (hl)
;	dec hl
;	dec hl
;	ENDM

;	@Par1 = column
ADD_BLK		MACRO	@Par1
	ld (hl),@Par1		; column
	inc hl
	ld (hl),b			; row
	inc hl
	ld (hl),a			; colorcode
	inc hl
	inc c
	ENDM

;	IN	HL = playfield
;		B = row, C = column (both start from 0)
;	OUT	HL -> playfield[r][c]
;	MOD	AF, HL
POINT_RC		MACRO
	ld a,b
	add a,a		; x2
	add a,a		; x4
	add a,a		; x8
	add a,b		; x9
	add a,b		; x10 PF_DIM_X DEPENDENT
	add a,c
	add a,l
	ld l,a
	ld a,0
	adc a,h
	ld h,a
	ENDM

EX_IXY	MACRO
	push ix
	push iy
	pop ix
	pop iy
    ENDM

; EOF
